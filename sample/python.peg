
TopLevel
    = << Stmt@+ #toplevel>>;

Stmt
    = IfStmt EOS?
    / WhileStmt EOS?
    / ForStmt EOS?
    / ReturnStmt EOS?
    / ImportStmt EOS?
    / BreakStmt EOS?
    / ContinueStmt EOS?
    / DefineFunction EOS?
    / DefineClass EOS?
    / Assert EOS?
    / Print EOS?
    / Raise EOS?
    / With EOS?
    / Expr EOS?
    / EOS+
    / catch .*
    ;

EOS
    = ";"
    / NL
    / "#" (!'\n' .)* '\n'
    ;

With
    = <<"with" WS+ Expr@ WS+ "as" WS+ Symbol@ WS* ":" NL+ Block@ #with>>
    ;

Assert
    = <<"assert" WS+ Expr@ #assert>>
    ;

Print
    = <<"print" WS+ Expr@ #print >>
    ;

Raise
    = <<"raise" WS+ Expr@ #throw >>
    ;

IfStmt
    = <<"if" WS+ Expr@ WS* ":" Block@ (indent _* "else" WS* ":" NL+ Block@)? #if>>
    ;

WhileStmt
    = <<"while" WS+ Expr@ WS* ":" Block@ #while>>
    ;

ForStmt
    = <<"for" WS+ Symbol@ WS+ "in" WS+ Expr@ ":" Block@ #foreach>>
    ;

ReturnStmt
    = <<"return" WS+ Expr@ #return>>
    ;

ImportStmt
    = <<"import" WS* ImportSymbol@ (WS* "," WS* Symbol@)* #import>>
    / <<"from" WS* Symbol@ WS* "import" WS* ImportSymbol@ (WS* "," WS* ImportSymbol@)* #fromImport>>
    ;

DefineFunction
    = <<"def" WS+ Symbol@ "(" Params@ ")" WS* ":"  Block@ #function>>
    ;

DefineClass
    = <<"class" WS+ Symbol@ (WS* "(" WS* (Extend@)? WS* ")" )? WS* ":" ClassBlock@ #class>>
    ;

Extend
    = << ExtendContent@ #extend >>
    ;

ExtendContent
    = Symbol@
    ;

BreakStmt
    = <<"break" #break>>
    ;

ContinueStmt
    = <<"continue" #continue>>
    ;

ReturnStmt
    = <<"return" WS+ Expr@ #return>>
    ;

Block
    = NL+ << (indent WS* Stmt@ NL?)+ #block>>
    / WS* << Stmt@ #block>>
    ;

ClassBlock
    = NL+ << (indent WS* ClassStmt@ NL?)+ #block>>;

ClassStmt
    = Stmt
    //= DefineFunction
    /// Expr
    ;

Expr
    = Assign
    ;

Assign
    = OrExpr <<@ WS* AssignOp WS* OrExpr@>>*
    ;

AssignOp
    = '=' !'=' #assign
    / '+='     #addeq
    / '-='     #subeq
    / '*='     #muleq
    / '/='     #diveq
    ;

OrExpr
    = AndExpr <<@ WS* 'or' WS* AndExpr@ #or >>*
    ;

AndExpr
    = BitwiseExpr <<@ WS* 'and' WS* BitwiseExpr@ #and >>*
    ;

BitwiseExpr
    = Equal <<@ WS* ('|' !'|' #bitor / '&' !'&' #bitand / '^' #bitxor) WS* Equal@ >>*
    ;

Equal
    = Relation <<@ WS*  EqOp@ WS* Relation@>>*
    ;

EqOp
    = 'is' WS+ 'not' #noteq
    / 'is' #eq
    / '==' #eq
    / '!=' #noteq
    ;

Relation
    = Shift <<@ WS* ('<' !'<' #lt / '<=' #lte / '>' !'>' #gt / '>=' #gte) WS* Shift@ >>*
    ;

Shift
    = Sum <<@ WS* ('<<' #lshift / '>>' #rshift) WS* Sum@ >>*
    ;

Sum
    = Product <<@ WS* ('+' #add / '-' #sub) WS* Product@ >>*
    ;

Product
    = Unary <<@ WS* ('*' #mul / '/' #div / '%' #mod) WS* Unary@ >>*
    ;

Unary
    = << ('not' #not / '!' #not / '-' #minus / '+' #plus / '~' #compl) Unary@ >>
    / Value <<@ Selector >>*
    ;

Selector
    = '.' Symbol@ #field
    / '[' WS* Expr@ WS* ']' #index
    / '(' << _* ( _* Expr@ ( _* ',' _* Expr@ )* )? #args >>@ ')'  #apply
    ;

Value
    //= << StringLiteral@ (_* StringLiteral@)+ #strings >>
    = StringLiteral
    / Symbol
    / Number
    / ArrayLiteral
    / MapLiteral
    / TupleLiteral
    ;

Assignment
    = Expr;

ArrayLiteral
    = << "[" _* Expr@? (_* "," _* Expr@)* _* "]" #array>>
    / << "[" _* Expr@ _* "for" _* Symbol@ _* "in" _* Expr@ (_* "if" _* Expr@)? _* "]" #listComprehension >>
    ;

MapLiteral
    = << "{" _* MapContent@? (_* "," _* MapContent@ )* _* "}" #map>>;

TupleLiteral
    = << '(' Expr@ ')' #group >>
    / << "(" _* Expr@ (_* "," _* Expr@)+ _* ")" #tuple>>
    ;

MapContent
    = << (StringLiteral / Symbol)@ _* ":" _* Expr@ #keyvalue>>;

Params
    = << Param@? (_* "," _* Param@)* _* #params>>;

Param
    = << Symbol@ (WS* '=' WS* Expr@)? #param >>
    ;

Args
    = << Expr@? (_* "," _* Expr@)* _* #args>>;

Symbol
    = << "True" #true >>
    / << "False" #false >>
    / << "None" #null >>
    / << [a-zA-Z_][a-zA-Z0-9_]* #name>>
    ;

StringLiteral
    = [bru]? '"""' << ('\\"' / !'"""' .)@*  #string >> '"""'
    / [bru]? "'''" << ("\\'" / !"'''" .)@*  #string >> "'''"
    / [bru]? '"' << ('\\"' / !'"' .)@* #string >> '"'
    / [bru]? "'" << ("\\'" / !"'" .)@* #string >> "'"
    ;

Number
    = << ('-' WS* / '+' WS*)?('0'/[1-9][0-9]*) '.' [0-9]+ #float >>
    / << ('-' WS* / '+' WS*)?('0'/[1-9][0-9]*) #integer>>
    ;

Asterisk
    = << '*' #asterisk>>
    ;

ImportSymbol
    = Symbol
    / Asterisk
    ;

WS
    = [ \t];

NL
    = [\n];

_
    = WS
    / NL
    ;
